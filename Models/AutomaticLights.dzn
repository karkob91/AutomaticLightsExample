import Utils/Timer/Timer.dzn;

interface ILightShifter {
    in void Toogle();

    behavior {
        on Toogle: {}
    }

}

interface IHighBeamsRelay {
    in void TurnOn();
    in void TurnOff();
    

    behavior {
        bool isTurnedOn = false;

        [isTurnedOn] {
            
            on TurnOff: {
                isTurnedOn = false;
            }
        }
        [!isTurnedOn] {
            on TurnOn: {
                isTurnedOn = true;
            }
            
        }
    }

}

interface ILightSensor {
    out void LowLight();
    out void HighLight();
    enum State {Low, High};

    behavior {
        State state = State.High;
        [state.High] {
            on inevitable: {
                state = State.Low;
                LowLight;
            }
        }
        [state.Low] {
            on inevitable: {
                state = State.High;
                HighLight;
            }
        }
    }
}

interface IFrontCamera {
    out void CarDetected();
    out void CarPassed();
    enum State {Detected, NotDetected};

    behavior {
        State state = State.NotDetected;

        [state.NotDetected] {
            on inevitable: {
                state = State.Detected;
                CarDetected;
            }
        }
        [state.Detected] {
            on inevitable: {
                state = State.NotDetected;
                CarPassed;
            }
        }
    }
}

// Special interface that is triggered every time
// to check if requirements conditions are met
interface Req {
    in void Req1();
    in void Req2();
    in void Req3();
    behavior {
        on Req1, Req2, Req3: {}
    }
}




component ModeSelector {
    provides ILightShifter lightShifter;
    requires IHighBeamsRelay beamsRelay;
    requires ILightSensor lightSensor;
    requires IFrontCamera frontCamera;
    provides Req req;
    requires Timer timer;

    behavior {
        //Requirement 1: If system is in the automatic mode and car is detected 
        //               in front, high beams should always be turned off 
        
        enum ControlMode {Automatic, Manual};
        ControlMode mode = ControlMode.Manual;
        int toggleDelay = $5000$;

        void ToggleLights() {
            if(beamsRelay.isTurnedOn)
                beamsRelay.TurnOff();
            else {
                beamsRelay.TurnOn();
            }
        }
        // Req1: If automatic mode is enabled systme should never blind a driver in front
        on req.Req1(): {
            if (mode.Automatic && frontCamera.state.Detected && beamsRelay.isTurnedOn) {
                illegal;
            }
        }
        // Req2: In automatic mode system should not turn on high beams if light conditions are high
        on req.Req2(): {
            if (mode.Automatic && lightSensor.state.High && beamsRelay.isTurnedOn && !timer.armed) {
                illegal;
            }
        }
        // Re3: When system is in manual mode and light is low, system should try enter Automatic mode
        on req.Req3(): {
            if (mode.Manual && lightSensor.state.Low && !timer.armed) {
                illegal;
            }
        }
        [mode.Manual] {
            on lightShifter.Toogle(): {
                ToggleLights();
                if(timer.armed)
                    timer.Cancel();
                if(lightSensor.state.Low) {
                    timer.Create(toggleDelay);
                }
            }
            on lightSensor.HighLight(): {
                if(timer.armed)
                    timer.Cancel();
            }
            on lightSensor.LowLight(): {
                timer.Create(toggleDelay);
            }
            on frontCamera.CarDetected(), frontCamera.CarPassed(): {}
            on timer.Timeout(): {
                mode = ControlMode.Automatic;
                if(!beamsRelay.isTurnedOn) {
                    if(frontCamera.state.NotDetected) {
                        beamsRelay.TurnOn();
                    }
                } else {
                    if(frontCamera.state.Detected) {
                        beamsRelay.TurnOff();
                    }
                }
            }
        }
        [mode.Automatic] {
            on lightShifter.Toogle(): {
                ToggleLights();
                if(timer.armed)
                    timer.Cancel();
                mode = ControlMode.Manual;
                if(lightSensor.state.Low) {
                    timer.Create(toggleDelay);
                }
            }
            on lightSensor.LowLight(): {
                if(timer.armed)
                    timer.Cancel();
                if(!beamsRelay.isTurnedOn && frontCamera.state.NotDetected) 
                    timer.Create(toggleDelay);
            }
            on lightSensor.HighLight(): {
                if(timer.armed)
                    timer.Cancel();
                if(beamsRelay.isTurnedOn) 
                    timer.Create(toggleDelay);
            }

            on timer.Timeout(): {
                ToggleLights();
            }

            on frontCamera.CarPassed(): {
                // if(timer.armed)
                //     timer.Cancel();
                if(!beamsRelay.isTurnedOn && lightSensor.state.Low) 
                    beamsRelay.TurnOn();
            }
            on frontCamera.CarDetected(): {
                if(timer.armed)
                    timer.Cancel();
                if(beamsRelay.isTurnedOn) 
                    beamsRelay.TurnOff();
            }
        }

        
    }

}